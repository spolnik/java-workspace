ext {
    libs = libsMap(
            jaxen: 'jaxen:jaxen:1.1.6',
            jdom: 'jdom:jdom:1.1',
            slf4j_api: 'org.slf4j:slf4j-api:1.7.10',
            log4j: 'log4j:log4j:1.2.17',
            slf4j_log4j12: 'org.slf4j:slf4j-log4j12:1.7.10',
            xstream: 'com.thoughtworks.xstream:xstream:1.4.8',
            wife: 'com.prowidesoftware:pw-swift-core:SRU2014-7.6',
            guava: 'com.google.guava:guava:19.0',
            h2: 'com.h2database:h2',
            hsqldb: 'org.hsqldb:hsqldb',
            activemq: 'org.apache.activemq:activemq-broker',
            jackson: 'com.fasterxml.jackson.core:jackson-databind',
            spring: [
                boot: 'org.springframework.boot:spring-boot-starter',
                batch: 'org.springframework.boot:spring-boot-starter-batch',
                context: 'org.springframework:spring-context',
                jms: 'org.springframework:spring-jms',
                jdbc: 'org.springframework:spring-jdbc',
                web: 'org.springframework:spring-web'
            ],
            test: [
                    junit: 'junit:junit:4.11',
                    assertj: 'org.assertj:assertj-core:1.7.1'
            ]
    )
}

def libsMap(Map<String, ?> map) {
    def processMap
    processMap = { innerMap ->
        innerMap.each {
            if (it.value instanceof Map) {
                innerMap[it.key] = processMap(it.value)
            }
        }
        innerMap.asImmutable().withDefault {
            throw new InvalidUserDataException(
                    "No entry '$it' in dependency map (candidates: ${innerMap.keySet().sort().join(", ")})" )
        }
    }
    processMap(map)
}